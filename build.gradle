buildscript {
    ext {
        kotlinVersion = '1.2.21'
        jerseyVersion = '3.0.2'
        shadowVersion = '2.0.2'
        jaxbVersion = '2.3.3'
    }
}
/**
 * This section is for all of the plugins we need to make this work.
 * Kotlin, the fat jar builder, and flag it
 * as an application so `./gradlew run` will work
 */
plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.21'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id "de.undercouch.download" version "4.1.1"
}
repositories {
    mavenLocal()
    mavenCentral()
}

apply plugin: 'application'

configurations {
    ktlint
}

/**
 * Standard dependency section for gradle.
 * Define the kotlin standard library for
 * Java 8.
 */

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.glassfish.jersey.containers:jersey-container-grizzly2-http:$jerseyVersion"
    implementation "org.glassfish.jersey.inject:jersey-hk2:$jerseyVersion"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion"
    implementation "org.glassfish.jaxb:jaxb-runtime:$jaxbVersion"

    ktlint "com.pinterest:ktlint:0.38.1"
}

/**
 * Personal preference. I hate having src/main/kotlin
 * be the root, so I change it that 'src'
 * as the root of my source directory.
 */
sourceSets {
    main.kotlin.srcDirs += 'src'
}

// Define the main startup class and jar name
mainClassName = 'ServerKt'
archivesBaseName = 'jersey-kotlin-demo'

// tell the jar which class to startup in.
jar {
    manifest {
        attributes 'Main-Class': 'ServerKt'
    }
}

task ktlint(type: JavaExec, group: "verifications"){
    description = "Verify Kotlin Code Style!"
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt", "--verbose"
}

task ktlintFormat(type: JavaExec, group: "formatting"){
  description = "Fix Kotlin code style!"
  main = "com.pinterest.ktlint.Main"
  classpath = configurations.ktlint
  args "-F", "src/**/*.kt"
}

// Properties for `./gradlew run`
run {
    standardInput = System.in
    environment = ["SHUTDOWN_TYPE" : "INPUT"]
}
